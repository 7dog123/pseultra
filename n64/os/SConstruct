import os

AddOption(
    '--prefix', 
    dest = 'prefix',
    type = 'string',
    nargs = 1,
    action = 'store',
    metavar = 'DIR',
    help = 'installation prefix'
)

env = Environment(PREFIX = GetOption('prefix'), ENV = os.environ)

env.Append(CPPPATH = ['#/include', '#/../../include'])

env.Replace(CC = 'mips64-gcc')
env.Replace(LD = 'mips64-ld')
env.Replace(AS = 'mips64-as')
env.Replace(STRIP = 'mips64-strip')

env.Replace(CCFLAGS = '-G0 -EB')
env.Replace(ASFLAGS = '-32 -G0 -EB -Iinclude -I../../include')

pseultra = env.Library(
    target = 'lib/libpseultra.a',
    source = [
        '#/src/event/event.c',
        '#/src/event/exception.c',
        '#/src/event/exception_s.s',
        '#/src/boot/boot.c',
        '#/src/boot/boot_s.s',
        '#/src/memory/malloc.c',
        '#/src/memory/memcpy.c',
    ] 
)

prefix = env.GetOption('prefix')

if prefix is None:
    prefix = '/opt/pseultra'

prefix_bin = prefix + '/bin'
prefix_include = prefix + '/include'
prefix_lib = prefix + '/lib'

env.Install(prefix_bin, [])
env.Install(prefix_include, ['#/include/os.h', '#/include/os'])
env.Install(prefix_lib, [pseultra])

install_bin = env.Alias('install-bin', prefix_bin)
install_lib = env.Alias('install-lib', prefix_lib)
install_include = env.Alias('install-include', prefix_include)

env.Alias('install', [install_bin, install_lib, install_include])
