import os

Import('mips_env')

path = Dir('.').abspath

mips_env.Append(CPPPATH = ['./include', './../../include'])

includes = [
    './include/os/boot.h',
    './include/os/event.h',
    './include/os/memory.h',
    './include/os/interfaces.h',
    './include/os/event/event.h',
    './include/os/event/exception.h',
    './include/os/memory/malloc.h',
    './include/os/memory/memcpy.h',
    './include/os/boot/boot.h',
    './include/os/interfaces/pi.h',
    './include/os/interfaces/vi.h',
]

sources = [
    './src/event/event.c',
    './src/event/exception.c',
    './src/event/exception_s.sx',
    './src/boot/boot.c',
    './src/boot/boot_s.sx',
    './src/memory/malloc.c',
    './src/memory/memcpy.c',
    './src/interfaces/pi.c',
    './src/interfaces/vi.c',
] + includes

pseultra = mips_env.Library(
    target = './lib/libpseultra.a',
    source = sources
)

prefix = mips_env.GetOption('prefix')

prefix_bin = prefix + '/bin'
prefix_include = prefix 
prefix_lib = prefix + '/lib'

includes += ['./include/os.sx', './include/os.h']

mips_env.Install(prefix_bin, [])
mips_env.Install(prefix_lib, [pseultra])
install_include = [mips_env.Install(prefix_include + '/' + os.path.dirname(h), h) for h in includes]

install_bin = mips_env.Alias('install-bin', prefix_bin)
install_lib = mips_env.Alias('install-lib', prefix_lib)
install_include = mips_env.Alias('install-include', install_include)

installs = [install_bin, install_lib, install_include]

mips_env.Alias('install', installs)
