/*
 * pseultra/n64/boot/ucode/setothermode.sx
 * PSM3D microcode SetOtherMode functions
 * 
 * (C) pseudophpt 2018
 */

//////////////////////////////////////////////////////////////////////////////
//                                                                          // 
// SetOtherMode                                                             // 
//                                                                          // 
// Sets modes associated with the Set_Other_Mode RDP command                // 
//                                                                          // 
// FORMAT:                                                                  // 
//                                                                          // 
// 02 MM MM MM 00 DD DD DD MM MM MM MM DD DD DD DD                          // 
//                                                                          // 
// M: Mask for D bits                                                       // 
//                                                                          // 
// D: Values of bits in high word to set                                    //
//                                                                          // 
////////////////////////////////////////////////////////////////////////////// 

.global OPSetOtherMode

OPSetOtherMode:

// Set high word

#define mask $t0
#define data $t1
#define mode $t2
#define modeword_ptr $s4

    or modeword_ptr, $zero, $zero

.SetWord:
    lw mask, %lo(cmd)($zero) // Load mask, data, and current mode for high word
    lw data, (%lo(cmd) + 4)($zero)
    lw mode, %lo(othermode)(modeword_ptr)

    and data, mask // Mask data

    nor mask, mask // Invert mask to mask mode
    and mode, mask 

    or mode, data // Put data together

    bnez modeword_ptr, .WriteCommand // If we're on the second word, we're ready to write the command
    sw mode, %lo(othermode)(modeword_ptr)

    move $a0, dl_dram_ptr // Load display list command
    ori $a1, $zero, %lo(cmd)
    jal DMARead // DMA command
    ori $a2, $zero, 7

    addiu dl_dram_ptr, 8 // Advance display list counter and modeword pointer
    b .SetWord
    addiu modeword_ptr, 4

#undef mode 
#undef data
#undef mask

// Actually write the command

#define cmd_ptr $a0
#define cmd_len $a1

.WriteCommand:
    ori cmd_ptr, $zero, othermode
    ori cmd_len, $zero, 8

#define set_other_mode_cmd $t0

    ori set_other_mode_cmd, $zero, UCODE_RDP_OPC_Set_Other_Modes 
    jal XBUSPush // push command to xbus
    sb set_other_mode_cmd, 0(cmd_ptr)

#undef set_other_mode_cmd

#undef cmd_len
#undef cmd_ptr

    op_ret
    nop
